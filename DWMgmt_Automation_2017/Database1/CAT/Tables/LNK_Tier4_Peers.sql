CREATE TABLE [CAT].[LNK_Tier4_Peers] (
    [T4P_ID]         INT      IDENTITY (1, 1) NOT NULL,
    [LNK_FK_T4_ID]   INT      NOT NULL,
    [LNK_FK_0400_ID] INT      NOT NULL,
    [LNK_FK_0401_ID] INT      NOT NULL,
    [LNK_FK_0402_ID] INT      CONSTRAINT [DF_T4P_FK_0402] DEFAULT ((0)) NOT NULL,
    [LNK_Post_Date]  DATETIME CONSTRAINT [DF_T4P_Post_Date] DEFAULT (getdate()) NOT NULL,
    [LNK_Term_Date]  DATETIME CONSTRAINT [DF_T4P_Term_Date] DEFAULT ('12/31/2099') NOT NULL,
    CONSTRAINT [PK_T4P] PRIMARY KEY CLUSTERED ([LNK_FK_T4_ID] ASC, [LNK_FK_0400_ID] ASC, [LNK_FK_0401_ID] ASC, [LNK_FK_0402_ID] ASC, [LNK_Term_Date] ASC) WITH (FILLFACTOR = 90),
    CONSTRAINT [CHK_FK_0400_ID_GT_0] CHECK ([LNK_FK_0400_ID]>(0)),
    CONSTRAINT [CHK_FK_T4_ID_GT_0] CHECK ([LNK_FK_T4_ID]>(0)),
    CONSTRAINT [FK_LNK_Tier4_Peers_LNK_0300_0400] FOREIGN KEY ([LNK_FK_T4_ID]) REFERENCES [CAT].[LNK_0300_0400_Object_Column_Collection] ([LNK_T4_ID]) ON DELETE CASCADE ON UPDATE CASCADE,
    CONSTRAINT [FK_LNK_Tier4_Peers_REG_0400_Column_Registry] FOREIGN KEY ([LNK_FK_0400_ID]) REFERENCES [CAT].[REG_0400_Column_Registry] ([REG_0400_ID]),
    CONSTRAINT [FK_LNK_Tier4_Peers_REG_0401_Column_Properties] FOREIGN KEY ([LNK_FK_0401_ID]) REFERENCES [CAT].[REG_0401_Column_Properties] ([REG_0401_ID]) ON DELETE CASCADE ON UPDATE CASCADE,
    CONSTRAINT [FK_LNK_Tier4_Peers_REG_0402_Column_Index_Details] FOREIGN KEY ([LNK_FK_0402_ID]) REFERENCES [CAT].[REG_0402_Column_Index_Details] ([REG_0402_ID]) ON DELETE CASCADE ON UPDATE CASCADE,
    CONSTRAINT [UQ_T4P_ID] UNIQUE NONCLUSTERED ([T4P_ID] ASC) WITH (FILLFACTOR = 90)
);


GO


CREATE TRIGGER [CAT].[TGR_T4_Current_Properties]
    ON  [CAT].[LNK_Tier4_Peers]
    INSTEAD OF INSERT
 AS  
 BEGIN
  SET NOCOUNT ON;
     
         
    UPDATE lnk SET LNK_Term_Date = getdate()
    FROM CAT.LNK_Tier4_Peers AS lnk
    LEFT JOIN Inserted AS i
    ON lnk.LNK_FK_T4_ID = i.LNK_FK_T4_ID
    AND lnk.LNK_FK_0400_ID = i.LNK_FK_0400_ID
    AND lnk.LNK_FK_0401_ID = i.LNK_FK_0401_ID
    AND lnk.LNK_FK_0402_ID = i.LNK_FK_0402_ID
    WHERE (lnk.LNK_Term_Date >= GETDATE()
	OR lnk.LNK_Term_Date < CAST(0 as datetime))
    AND i.LNK_FK_T4_ID IS NULL


    INSERT INTO CAT.LNK_Tier4_Peers (LNK_FK_T4_ID, LNK_FK_0400_ID, LNK_FK_0401_ID, LNK_FK_0402_ID)
    
    SELECT i.LNK_FK_T4_ID, i.LNK_FK_0400_ID, i.LNK_FK_0401_ID, i.LNK_FK_0402_ID
    FROM Inserted AS i
	LEFT JOIN CAT.LNK_Tier4_Peers AS p
	ON p.LNK_FK_T4_ID = i.LNK_FK_T4_ID
	AND p.LNK_FK_0400_ID = i.LNK_FK_0400_ID
	AND p.LNK_FK_0401_ID = i.LNK_FK_0401_ID
	AND p.LNK_FK_0402_ID = i.LNK_FK_0402_ID
	AND p.LNK_Term_Date >= GETDATE()
	WHERE p.T4P_ID IS NULL
     
 END